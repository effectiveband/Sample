apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        warning 'InvalidPackage'
    }

    kotlinOptions {
        freeCompilerArgs += [
                "-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi"]
        jvmTarget = '1.8'
    }

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionName rootProject.ext.versionName
        versionCode rootProject.ext.versionCode

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "default", "drawer"

    productFlavors {
        dev {
            dimension "default"
        }

        prod {
            dimension "default"
        }

        withDrawer {
            dimension "drawer"
        }

        noDrawer {
            dimension "drawer"
        }
    }

    variantFilter { variant ->
        def name = variant.name
        def isNotDevDebugBuild = !name.contains("dev") && name.contains("Debug")
        def isPublicBuildDDrawer = name.contains("prod") && !name.contains("NoDDrawer")
        if (isNotDevDebugBuild || isPublicBuildDDrawer) {
            setIgnore(true)
        }
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
    }
}

dependencies {
    implementation libs.bundles.kotlin
    implementation libs.bundles.kotlinx
    implementation libs.bundles.androidx
    implementation libs.material
    implementation libs.glide

    implementation libs.timber

    implementation libs.dagger.annotations
    kapt libs.dagger.processor

    testImplementation libs.bundles.test
    androidTestImplementation libs.bundles.android.test
}