plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "band.effective.headlines"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionName rootProject.ext.versionName
        versionCode rootProject.ext.versionCode

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default", "drawer"

    productFlavors {
        dev {
            dimension "default"
        }

        prod {
            dimension "default"
        }

        withDrawer {
            dimension "drawer"
        }

        noDrawer {
            dimension "drawer"
        }
    }

    variantFilter { variant ->
        def name = variant.name
        def isNotDevDebugBuild = !name.contains("dev") && name.contains("Debug")
        def isPublicBuildDDrawer = name.contains("prod") && !name.contains("NoDrawer")
        if (isNotDevDebugBuild || isPublicBuildDDrawer) {
            setIgnore(true)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation projects.core
    implementation projects.coreui
    implementation projects.debug

    implementation projects.main
    implementation projects.mainNavigation

    implementation projects.feed
    implementation projects.about
    implementation projects.search

    implementation projects.network
    implementation projects.newsApi

    implementation libs.bundles.kotlin
    implementation libs.bundles.kotlinx
    implementation libs.bundles.androidx
    implementation libs.material
    implementation libs.cicerone
    implementation libs.moshi.core
    implementation libs.retrofit

    implementation libs.dagger.annotations
    kapt libs.dagger.processor

    testImplementation libs.bundles.test
    androidTestImplementation libs.bundles.android.test
}
